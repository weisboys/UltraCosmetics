plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    // Depend on a jar in the 'core' folder: fileTree(dir: '.', include: ['*.jar'])
    // ideally matches the dependency version of the latest NMS module
    compileOnly 'org.spigotmc:spigot-api:1.20.3-R0.1-SNAPSHOT'
    compileOnly('LibsDisguises:LibsDisguises:10.0.42') {
        exclude group: 'org.spigotmc'
        exclude group: 'com.comphenix.protocol'
    }
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'org.black_ixx:playerpoints:3.2.6'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.3'
    compileOnly 'com.mojang:authlib:2.3.31'
    // used by the update checker
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
    // NMS modules can't actually use XSeries but it doesn't compile without 'api'.
    // IDEs do not like it when 'core' references XSeries directly
    // but subprojects must reference XSeries as shaded.
    //api 'com.github.cryptomorin:XSeries:9.9.0'
    api fileTree(dir: '.', include: ['XSeries-*-datatags.jar'])
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly "me.lokka30:treasury-api:2.0.2-SNAPSHOT"
    compileOnly 'de.jeff_media:ChestSortAPI:13.0.0-SNAPSHOT'
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.1.22'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'net.wesjd:anvilgui:1.9.2-SNAPSHOT'
    implementation 'me.gamercoder215:mobchip-bukkit:1.9.5-SNAPSHOT'
    compileOnly 'com.discordsrv:discordsrv:1.27.1-SNAPSHOT'
    compileOnly 'me.soknight:peconomy:2.7.4-pre1'
    implementation "net.kyori:adventure-text-minimessage:4.16.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.2"
    implementation "net.kyori:adventure-text-serializer-plain:4.16.0"
    api "net.kyori:adventure-api:4.16.0"
    compileOnly "com.olziedev:playerauctions-api:1.28.1"
}
shadowJar {
    // Causes shadow jar to overwrite normal jar, which makes it work with Jitpack
    archiveClassifier = ''
    // removes unused classes in 'implementation' path dependencies
    minimize {
        // Loads classes reflectively
        exclude(dependency('net.wesjd:anvilgui:.*'))
        exclude(dependency('me.gamercoder215:.*:.*'))
    }
    def path = 'be.isach.ultracosmetics.shaded'
    // XParticle is big and not required so don't shade it.
    // It's not removed by minimize() because it's on the API path I guess?
    exclude 'com/cryptomorin/xseries/particles/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_13_R1/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_16_R1/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_16_R2/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_18_R1/*'
    relocate('com.zaxxer.hikari', path + '.hikari')
    relocate('com.cryptomorin.xseries', path + '.xseries')
    relocate('org.bstats', path + '.metrics')
    relocate('net.wesjd', path + '.anvilgui')
    relocate('me.gamercoder215.mobchip', path + '.mobchip')
    relocate('net.kyori', path + '.kyori')
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId project.group
            //artifactId project.name
            version project.version
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
