plugins {
    id 'io.github.goooler.shadow' version '8.1.7'
    id("net.minecrell.plugin-yml.bukkit").version("0.6.0")
}

bukkit {
    name = "UltraCosmetics"
    authors = ['datatags']
    main = 'be.isach.ultracosmetics.UltraCosmetics'
    apiVersion = "1.17" // Earliest version to support Java 17
    softDepend = ['LibsDisguises', 'Vault', 'PlaceholderAPI', 'PlayerPoints', 'WorldGuard', 'Essentials', 'LuckPerms', 'DiscordSRV', 'ChestSort', 'Towny', 'PEconomy', 'PlayerAuctions', 'CoinsEngine', 'Treasury']
    commands {
        ultracosmetics {
            aliases = ['uc', 'cosmetics']
            description = 'Main command of UltraCosmetics'
            usage = '/<command>'
        }
    }
    permissions {
        'ultracosmetics.openmenu' {
            setDefault('TRUE')
        }
        'ultracosmetics.receivechest' {
            setDefault('TRUE')
        }
        'ultracosmetics.treasurechests.buykey' {
            setDefault('TRUE')
        }
        'ultracosmetics.bypass.disabledcommands' {
            setDefault('OP')
        }
        'ultracosmetics.bypass.cooldown' {
            setDefault('FALSE')
        }
    }
}

dependencies {
    // Depend on a jar in the 'core' folder: fileTree(dir: '.', include: ['*.jar'])
    // ideally matches the dependency version of the latest NMS module
    compileOnly 'org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT'
    compileOnly('LibsDisguises:LibsDisguises:10.0.44') {
        exclude group: 'org.spigotmc'
        exclude group: 'com.comphenix.protocol'
    }
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'org.black_ixx:playerpoints:3.2.7'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'
    compileOnly 'com.mojang:authlib:2.3.31'
    // used by the update checker
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
    library 'com.github.cryptomorin:XSeries:11.0.0'
    //api fileTree(dir: '.', include: ['XSeries-*.jar'])
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly "me.lokka30:treasury-api:2.0.2-SNAPSHOT"
    compileOnly 'de.jeff_media:ChestSortAPI:13.0.0-SNAPSHOT'
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.3.1'
    library 'com.zaxxer:HikariCP:5.1.0'
    implementation 'net.wesjd:anvilgui:1.9.4-SNAPSHOT' // CodeMC repo
    implementation 'com.github.datatags.MobChipLite:mobchip-bukkit:1.10.4'
    implementation fileTree(dir: '.', include: ['mobchip-*.jar'])
    compileOnly 'com.discordsrv:discordsrv:1.28.0-SNAPSHOT'
    compileOnly 'me.soknight:peconomy:2.7.4-pre1'
    library "net.kyori:adventure-text-minimessage:4.17.0"
    library "net.kyori:adventure-platform-bukkit:4.3.3"
    library "net.kyori:adventure-text-serializer-plain:4.17.0"
    library "net.kyori:adventure-api:4.17.0"
    compileOnly "com.olziedev:playerauctions-api:1.28.1"
    compileOnly files('libs/CoinsEngine-2.1.1.jar')
    compileOnly files('libs/NexEngine.jar')
    compileOnly(project(":legacy-events")) // Shaded by root project
}

shadowJar {
    // Causes shadow jar to overwrite normal jar, which makes it work with Jitpack
    archiveClassifier = ''
    // removes unused classes in 'implementation' path dependencies
    minimize {
        // Loads classes reflectively
        exclude(dependency('com.github.datatags.MobChipLite:.*:.*'))
    }
    def path = 'be.isach.ultracosmetics.shaded'
    relocate('org.bstats', path + '.metrics')
    relocate('net.wesjd', path + '.anvilgui')
    relocate('me.gamercoder215.mobchip', path + '.mobchip')
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId project.group
            //artifactId project.name
            version project.version
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
