plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    // Depend on a jar in the 'core' folder: fileTree(dir: '.', include: ['*.jar'])
    // ideally matches the dependency version of the latest NMS module
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly('LibsDisguises:LibsDisguises:10.0.37') {
        exclude group: 'org.spigotmc'
        exclude group: 'com.comphenix.protocol'
    }
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'org.black_ixx:playerpoints:3.2.6'
    // WG 7.0.3 was last version to support Java <16
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.3'
    // same version used in 1.17.1, 3.x uses Java 16
    compileOnly 'com.mojang:authlib:2.3.31'
    // used by the update checker
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
    // NMS modules can't actually use XSeries but it doesn't compile without 'api'.
    // IDEs do not like it when 'core' references XSeries directly
    // but subprojects must reference XSeries as shaded.
    api 'com.github.cryptomorin:XSeries:9.4.0'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly "me.lokka30:treasury-api:2.0.2-SNAPSHOT"
    compileOnly 'de.jeff_media:ChestSortAPI:13.0.0-SNAPSHOT'
    compileOnly 'com.palmergames.bukkit.towny:towny:0.99.5.4'
    // Last version to support Java 8
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'net.wesjd:anvilgui:1.7.0-SNAPSHOT'
    //implementation 'me.gamercoder215:mobchip-bukkit:1.9.0-SNAPSHOT'
    implementation(fileTree(dir: '.', include: ['mobchip-bukkit-*.jar']))
    compileOnly 'com.discordsrv:discordsrv:1.27.0-SNAPSHOT'
    compileOnly 'me.soknight:peconomy:2.7.4-pre1'
    implementation "net.kyori:adventure-text-minimessage:4.14.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.0"
    implementation "net.kyori:adventure-text-serializer-plain:4.14.0"
    compileOnly "com.olziedev:playerauctions-api:1.24.0-pre2"
}
shadowJar {
    // removes unused classes in 'implementation' path dependencies
    /*minimize {
        // Loads classes reflectively
        exclude(dependency('net.wesjd:anvilgui:.*'))
        exclude(dependency('me.gamercoder215:.*:.*'))
    }*/
    def path = 'be.isach.ultracosmetics.shaded'
    // XParticle is big and not required so don't shade it.
    // It's not removed by minimize() because it's on the API path I guess?
    exclude 'com/cryptomorin/xseries/particles/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_13_R1/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_16_R1/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_16_R2/*'
    exclude 'me/gamercoder215/mobchip/abstraction/v1_18_R1/*'
    relocate('com.zaxxer.hikari', path + '.hikari')
    relocate('com.cryptomorin.xseries', path + '.xseries')
    relocate('org.bstats', path + '.metrics')
    relocate('net.wesjd', path + '.anvilgui')
    relocate('me.gamercoder215.mobchip', path + '.mobchip')
    relocate('net.kyori.adventure', path + '.adventure')
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
